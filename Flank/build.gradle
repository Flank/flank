plugins {
    // https://github.com/diffplug/spotless/tree/master/plugin-gradle
    id 'com.diffplug.gradle.spotless' version '3.4.0'
}

repositories {
    jcenter()
}

group 'com.walmart.otto'
version '2.0.2'

apply plugin: 'java'
apply plugin: 'findbugs'

tasks.withType(FindBugs) {
    reports {
        xml.enabled false
        html.enabled true
    }
}

dependencies {
    compile 'net.sf.jopt-simple:jopt-simple:4.9'
    compile 'com.linkedin.dextestparser:parser:1.1.0'
    compile 'com.samskivert:jmustache:1.13'
    compileOnly 'com.google.code.findbugs:annotations:3.0.1'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

jar {
    dependsOn configurations.runtime
    from {
        (configurations.runtime).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    manifest {
        attributes 'Main-Class': 'com.walmart.otto.Flank'
    }
}

apply plugin: 'com.diffplug.gradle.spotless'
spotless {
    java {
        googleJavaFormat()
    }
}
